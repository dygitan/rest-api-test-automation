buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.4.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'rest-api-test-automation'
    version = '0.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'net.masterthought:cucumber-reporting:4.7.0'
    compile 'junit:junit:4.12'
    compile 'com.intuit.karate:karate-junit4:0.9.2'
    compile 'com.intuit.karate:karate-apache:0.9.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

clean {
    doFirst {
        delete("results")
        println "[results] directory deleted!"
    }
}

test {
    include '**/*Test*'
    outputs.upToDateWhen { false }
}

task printTestReportLink {
    doLast {
        def fileLoc = "file://${rootProject.projectDir}/results/cucumber-html-reports/overview-features.html"
        println "To view test report, click " + new URI(fileLoc)
    }
}

test.finalizedBy(printTestReportLink)


